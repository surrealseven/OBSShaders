// Always provided by OBS
uniform float4x4 ViewProj<
	bool automatic = true;
	string name = "View Projection Matrix";
>;

// Provided by Stream Effects
uniform float4 Time<
    // x: Time in seconds since the source was created.
    // y: Time in the current second.
    // z: Total seconds passed since the source was created.
    // w: Reserved
	bool automatic = true;
    string name = "Time Array";
	string description = "A float4 value containing the total time, rendering time and the time since the last tick. The last value is a random number between 0 and 1.";
>;
uniform float4 ViewSize<
    // x: Width
    // y: Height
    // z: 1. / Width
    // w: 1. / Height
	bool automatic = true;
>;
uniform texture2d InputA<
	bool automatic = true;
>;
uniform float4x4 Random<
	bool automatic = true;
	string name = "Random Array";
	string description = "A float4x4 value containing random values between 0 and 1";
>;


// Shader Parameters
uniform int ArmCount <
	string name = "Arm Count";
	string field_type = "slider";
	int minimum = 1;
	int maximum = 30;	
> = 1;

uniform float Linearity <
	string name = "Thickness Linearity";
	string field_type = "slider";
	float minimum = 0.01;
	float maximum = 2;
	float step = 0.01;
> = 1;

uniform float Rotation <
	string name = "Rotation";
	string field_type = "slider";
	float minimum = 0;
	float maximum = 360;
	float step = 0.01;
> = 0;

uniform float Speed <
	string name = "Rotation Speed";
	string field_type = "slider";
	float minimum = -20;
	float maximum = 20;
	float step = 0.01;
> = 0;

uniform float Smooth <
	string name = "Border Smoothing";
	string field_type = "slider";
	float minimum = 0;
	float maximum = 1;
	float step = 0.01;
> = 0.13;

uniform float CenterX <
	string name = "Center X";
	string field_type = "slider";
	float minimum = 0;
	float maximum = 1;
	float step = 0.001;
> = 0.5;

uniform float CenterY <
	string name = "Center Y";
	string field_type = "slider";
	float minimum = 0;
	float maximum = 1;
	float step = 0.001;
> = 0.5;

uniform float Curve <
	string name = "Spiral Curve";
	string field_type = "slider";
	float minimum = 0.1;
	float maximum = 100;
	float step = 0.01;
> = 10;

uniform float Handedness <
	string name = "Handedness";
	string field_type = "enum";
	float enum_0 = 0;
	string enum_0_name = "Right";
	float enum_1 = 1;
	string enum_1_name = "Left";
> = 0;


#define PI		3.14159265359
#define DEG2RAD	PI / 180


// ---------- Shader Code
sampler_state def_sampler{
	AddressU = Clamp;
	AddressV = Clamp;
	Filter = Linear;
};

struct VertData {
	float4 pos : POSITION;
	float2 uv : TEXCOORD0;
};

VertData VSDefault(VertData v_in)
{
	VertData vert_out;
	vert_out.pos = mul(float4(v_in.pos.xyz, 1.0), ViewProj);
	vert_out.uv = v_in.uv;
	return vert_out;
}




float4 CalculateColor(float2 position, float2 center)
{
	float2 dir = position - center;

	float theta = (atan2(dir.x, dir.y) / PI) * 0.5 + 0.5;
	theta = abs(Handedness - theta);
	theta = frac(theta + Rotation / 360);

	float dist = pow(length(dir), Linearity) * Curve + theta * (ArmCount * 2);

	float t = abs(fmod(dist - Time.x * Speed, 2));

	float border = (1.0 - Smooth) * 0.5;
	float color = smoothstep(border, 1 - border, t) * smoothstep(border, 1 - border, 2 - t);

	return float4(color, color, color, 1);

}


float4 PSDefault(VertData v_in) : TARGET
{
	float aspect = ViewSize.x / ViewSize.y;

	float2 uv = v_in.uv * float2(aspect, 1);

	float4 color = 0;

	color = CalculateColor(uv, float2(CenterX * aspect, CenterY));
	color.a = 1;

	return color;
}

technique Draw
{
	pass
	{
		vertex_shader = VSDefault(v_in);
		pixel_shader = PSDefault(v_in);
	}
}
