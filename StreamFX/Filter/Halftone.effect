// include filter shader variables
#include "SrlFilterEffect.h"


uniform float MinDotSize <
	string name = "Minimum Dot Size";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 128;
> = 10;

uniform float MaxDotSize <
	string name = "Maximum Dot Size";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 128;
> = 30;

uniform float DotSmooth <
	string name = "Dot Smoothing";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 64;
> = 10;

uniform float DotBloom <
	string name = "Dot Bloom";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 64;
> = 0;

uniform float Intensity <
	string name = "Intensity";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0.25;
	float maximum = 4.0;
> = 1;

uniform float Opacity <
	string name = "Opacity";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 1;
> = 1;

uniform float Strength <
	string name = "Strength";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 1;
> = 1;

uniform float Angle <
	string name = "Angle";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 1;
> = 0.1;

uniform float CAngle <
	string name = "C Angle";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 1;
> = 0.06;

uniform float MAngle <
	string name = "M Angle";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 1;
> = 0.18;

uniform float YAngle <
	string name = "Y Angle";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 1;
> = 0.14;

uniform float KAngle <
	string name = "K Angle";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 1;
> = 0.03;

uniform float RAngle <
	string name = "R Angle";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 1;
> = 0.05;

uniform float GAngle <
	string name = "G Angle";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 1;
> = 0.15;

uniform float BAngle <
	string name = "B Angle";
	string field_type = "slider";
	float step = 0.01;
	float minimum = 0;
	float maximum = 1;
> = 0.25;






// ---------- Shader Code
sampler_state def_sampler {
	AddressU  = Clamp;
	AddressV  = Clamp;
	Filter    = Linear;
};

struct VertData {
	float4 pos : POSITION;
	float2 uv  : TEXCOORD0;
};

VertData VSDefault(VertData v_in) 
{
	VertData vert_out;
	vert_out.pos = mul(float4(v_in.pos.xyz, 1.0), ViewProj);
	vert_out.uv  = v_in.uv;
	return vert_out;
}

float2x2 CreateRotation(float theta)
{
	float cosTheta = cos(theta);
	float sinTheta = sin(theta);
	return float2x2(cosTheta, -sinTheta, sinTheta, cosTheta);
}

struct ColorData
{
	float4 color;
	float dist;
};

ColorData FetchColor(float2 uv, float angle)
{
	float aspect = ViewSize.x / ViewSize.y;
	float tileSize = max(MinDotSize, MaxDotSize);

	float2x2 rot = CreateRotation(angle * PI);
	float2x2 invRot = CreateRotation(-angle * PI);

	float2 scale = ViewSize.zz * tileSize;
	float2 rotatedUv = mul(rot, float2(uv.x * aspect, uv.y));
	float2 tileCenter = (floor(rotatedUv / scale) + 0.5) * scale;
	float dist = distance(rotatedUv, tileCenter);
	tileCenter = mul(invRot, tileCenter);
	tileCenter.x *= 1 / aspect;

	ColorData cd;
	cd.color = InputA.Sample(def_sampler, tileCenter);
	cd.color.a = 1;
	cd.dist = dist;
	return cd;
}

float CalculateIntensity(float dist, float t)
{
	t = pow(saturate(t), Intensity);
	return smoothstep(dist - ViewSize.z * DotBloom, dist + ViewSize.z * DotSmooth, lerp(MinDotSize, MaxDotSize, t) * ViewSize.z * 0.5);
}

float4 CalculateComposite(float3 rgb, float2 uv)
{
	float4 rgba = float4(rgb.r, rgb.g, rgb.b, Opacity);
	return lerp(InputA.Sample(def_sampler, uv), rgba, Strength);
}


// luma
float4 PSLuma(VertData v_in) : TARGET
{
	ColorData cd = FetchColor(v_in.uv, Angle);

	float value = CalculateIntensity(cd.dist, RgbToLuma(cd.color));

	return CalculateComposite(float3(value, value, value), v_in.uv);
}

technique Luma
{
	pass
	{
		vertex_shader = VSDefault(v_in);
		pixel_shader = PSLuma(v_in);
	}
}


// rgb
float4 PSRgb(VertData v_in) : TARGET
{
	ColorData cdR = FetchColor(v_in.uv, RAngle);
	ColorData cdG = FetchColor(v_in.uv, GAngle);
	ColorData cdB = FetchColor(v_in.uv, BAngle);

	float r = cdR.color.r;
	float g = cdG.color.g;
	float b = cdB.color.b;

	r = CalculateIntensity(cdR.dist, r);
	g = CalculateIntensity(cdG.dist, g);
	b = CalculateIntensity(cdB.dist, b);

	return CalculateComposite(float3(r, g, b), v_in.uv);
}

technique RGB
{
	pass
	{
		vertex_shader = VSDefault(v_in);
		pixel_shader = PSRgb(v_in);
	}
}


// cmy
float4 PSCmy(VertData v_in) : TARGET
{
	ColorData cdC = FetchColor(v_in.uv, CAngle);
	ColorData cdM = FetchColor(v_in.uv, MAngle);
	ColorData cdY = FetchColor(v_in.uv, YAngle);

	float c = RgbToCmy(cdC.color.rgb).x;
	float m = RgbToCmy(cdM.color.rgb).y;
	float y = RgbToCmy(cdY.color.rgb).z;

	c = CalculateIntensity(cdC.dist, c);
	m = CalculateIntensity(cdM.dist, m);
	y = CalculateIntensity(cdY.dist, y);

	return CalculateComposite(CmyToRgb(float3(c, m, y)), v_in.uv);
}

technique CMY
{
	pass
	{
		vertex_shader = VSDefault(v_in);
		pixel_shader = PSCmy(v_in);
	}
}

// cmyk
float4 PSCmyk(VertData v_in) : TARGET
{
	ColorData cdC = FetchColor(v_in.uv, CAngle);
	ColorData cdM = FetchColor(v_in.uv, MAngle);
	ColorData cdY = FetchColor(v_in.uv, YAngle);
	ColorData cdK = FetchColor(v_in.uv, KAngle);

	float c = RgbToCmyk(cdC.color.rgb).x;
	float m = RgbToCmyk(cdM.color.rgb).y;
	float y = RgbToCmyk(cdY.color.rgb).z;
	float k = RgbToCmyk(cdK.color.rgb).w;

	c = CalculateIntensity(cdC.dist, c);
	m = CalculateIntensity(cdM.dist, m);
	y = CalculateIntensity(cdY.dist, y);
	k = CalculateIntensity(cdK.dist, k);

	return CalculateComposite(CmykToRgb(float4(c, m, y, k)), v_in.uv);
}

technique CMYK
{
	pass
	{
		vertex_shader = VSDefault(v_in);
		pixel_shader = PSCmyk(v_in);
	}
}




